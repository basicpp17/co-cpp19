@PACKAGE_INIT@

set(_CoCpp19_coro19_dependencies array19)
set(_CoCpp19_enum19_dependencies string19)
set(_CoCpp19_meta19_dependencies array19)
set(_CoCpp19_optional19_dependencies meta19)
set(_CoCpp19_serialize19_dependencies meta19)
set(_CoCpp19_signal19_dependencies array19)
set(_CoCpp19_string19_dependencies tuple19)
set(_CoCpp19_stong19_dependencies string19)
set(_CoCpp19_variant19_dependencies meta19)
set(_CoCpp19_tuple19_dependencies meta19)

function(_cocpp19_include_component components)
  foreach(_comp ${components})
    if (EXISTS "${CMAKE_CURRENT_LIST_DIR}/CoCpp19-${_comp}-targets.cmake")
      if (DEFINED _CoCpp19_${_comp}_dependencies)
        _cocpp19_include_component("${_CoCpp19_${_comp}_dependencies}")
      endif()
      include("${CMAKE_CURRENT_LIST_DIR}/CoCpp19-${_comp}-targets.cmake")
      set(CoCpp19_${_comp}_FOUND ON PARENT_SCOPE)
    else()
      if (DEFINED _CoCpp19_missing_components)
        list(APPEND _CoCpp19_missing_components "${_comp}")
        set(_CoCpp19_missing_components "${_CoCpp19_missing_components}" PARENT_SCOPE)
      else()
        set(_CoCpp19_missing_components "${_comp}")
        set(_CoCpp19_missing_components "${_CoCpp19_missing_components}" PARENT_SCOPE)
      endif ()
    endif()
  endforeach()
endfunction()

_cocpp19_include_component("${CoCpp19_FIND_COMPONENTS}")
check_required_components(CoCpp19)

if ((NOT CoCpp19_FOUND) AND (NOT DEFINED CoCpp19_NOT_FOUND_MESSAGE) AND (DEFINED _CoCpp19_missing_components))
  list(REMOVE_DUPLICATES _CoCpp19_missing_components)
  list(JOIN _CoCpp19_missing_components " " _CoCpp19_missing_components)
  set(CoCpp19_NOT_FOUND_MESSAGE "Unsupported components: ${_CoCpp19_missing_components}")
endif()
